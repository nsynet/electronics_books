;说明使用到的外部函数和外部变量
EXTRN	CODE(CWE1, DWE1, CWE2, DWE2)
EXTRN	DATA(COM, DAT)

;定义全局函数INITIAL(), CLEAR()
PUBLIC	INITIAL, CLEAR
INITP	SEGMENT	CODE
INIROM	SEGMENT CODE
RSEG	INIROM

;定义内部变量：初始化变量数组TABLE
TABLE:	DB	0E2H, 0A4H, 0A9H, 0A0H, 0C0H, 0AFH
RSEG	INITP

;INITIAL()初始化控制器E1, E2
INITIAL:	PUSH	DPH
		PUSH	DPL
		MOV 	R4, 		#0
INIT01:		MOV	DPTR, 		#TABLE
		MOV	A, 		R4
		MOVC	A, 		@A+DPTR
		MOV	COM, 		A
		LCALL	CWE1
		LCALL	CWE2
		INC	R4
		CJNE	R4, 		#6H, INIT01
		POP	DPL
		POP	DPH
		RET

;清屏，即所有数据单元清0，流程图参见图2-25。
CLEAR:		MOV	R4,		#00H
CLEAR1:		MOV	A,		R4
		ORL	A,		#0B8H
		MOV	COM,		A
        	LCALL   CWE1
       		LCALL   CWE2
		MOV	COM,		#00H
        	LCALL   CWE1
        	LCALL   CWE2
		MOV	R3,		#3CH
CLEAR2:		MOV	DAT,		#00H
        	LCALL   DWE1
        	LCALL   DWE2
		DJNZ	R3,		CLEAR2
		INC	R4
		CJNE	R4,		#04H,	CLEAR1
		RET
		END
